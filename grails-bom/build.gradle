plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

def plugins = new Properties()
plugins.load(new StringReader(new File("$projectDir/plugins.properties").text))

def profiles = new Properties()
profiles.load(new StringReader(new File("$projectDir/profiles.properties").text))

ext {
    // When making changes in the dependencyVersions, remember to also update the Grails BOM Documentation:
    // https://docs.grails.org/snapshot/ref/Dependency%20Versions/Grails%20BOM.html
    dependencyVersions = """\
        com.fasterxml.jackson.core:jackson-databind::$jacksonVersion
        com.github.ben-manes.caffeine:caffeine::$caffeineVersion
        com.github.javaparser:javaparser-core::$javaParserCoreVersion
        com.h2database:h2::$h2Version
        commons-codec:commons-codec::$commonsCodecVersion
        hsqldb:hsqldb::$hsqldbVesion
        io.methvin:directory-watcher::$directoryWatcherVersion
        jakarta.annotation:jakarta.annotation-api::$jakartaAnnotationApiVersion
        jakarta.inject:jakarta.inject-api::$jakartaInjectVersion
        jakarta.persistence:jakarta.persistence-api::$jakartaPersistenceVersion
        jakarta.servlet:jakarta.servlet-api::$servletApiVersion
        jakarta.xml.bind:jakarta.xml.bind-api::$jakartaXmlBindVersion
        jline:jline::$jlineVersion
        net.bytebuddy:byte-buddy::$bytebuddyVersion
        net.java.dev.jna:jna::$jnaVersion
        org.apache.ant:ant:,junit:$antVersion
        org.apache.commons:commons-text::$commonsTextVersion
        org.apache.groovy:groovy:ant,cli-picocli,console,dateutil,json,macro,nio,sql,swing,templates,test,test-junit5,xml:$groovyVersion
        org.apache.tomcat:tomcat-jdbc::$tomcatVersion
        org.asciidoctor:asciidoctorj::$asciidoctorjVersion
        org.aspectj:aspectjrt,aspectjweaver::$aspectjVersion
        org.fusesource.jansi:jansi::$jansiVersion
        com.github.spotbugs:spotbugs-annotations::$jsr305Version
        org.grails.plugins:async,events::$asyncVersion
        org.grails.plugins:converters::$legacyConvertersVersion
        org.grails.plugins:gsp::$gspVersion
        org.grails:grails-async,grails-events:gpars,rxjava,rxjava2:$asyncVersion
        org.grails:grails-datastore-gorm-hibernate5::$hibernateDatastoreVersion
        org.grails:grails-datastore:async,core,gorm,gorm-async,gorm-support,gorm-rx,gorm-test,gorm-validation,web:$datastoreVersion
        org.grails:grails-gdoc-engine::$gdocEngineVersion
        org.grails:grails-gradle-plugin::$grailsGradlePluginVersion
        org.grails:grails-testing-support,grails-gorm-testing-support,grails-web-testing-support::$testingSupportVersion
        org.grails:grails:gsp,web-gsp:$gspVersion
        org.grails:views-json-testing-support::$viewsVersion
        org.hamcrest:hamcrest-core::$hamcrestVersion
        org.jsoup:jsoup::$jsoupVersion
        org.junit.jupiter:junit-jupiter:api,engine:$junitVersion
        org.junit.platform:junit-platform-runner::$junitPlatformRunnerVersion
        org.mongodb:bson:,record-codec:$mongodbJavaDriverVersion
        org.mongodb:mongodb-driver:core,sync:$mongodbJavaDriverVersion
        org.objenesis:objenesis::$objenesisVersion
        org.slf4j:jcl-over-slf4j::$slf4jVersion
        org.slf4j:slf4j:api,simple::$slf4jVersion
        org.spockframework:spock,spock:core,spring:$spockVersion
        org.springframework.boot:spring-boot-cli::$springBootVersion
        org.springframework:springloaded::$springLoadedVersion
        org.xhtmlrenderer:flying-saucer-pdf-openpdf::$xhtmlRendererVersion
        org.yaml:snakeyaml::$snakeyamlVersion
    """.trim().readLines().collect { it.trim() }.collect {
        def info = it.toString().split(':')
        [group: info[0], names: info[1].split(','), modules: info[2].split(','), version: info[3]]
    }.collectEntries {
        def depList = GroovyCollections
                .combinations(it.names, it.modules)
                .collect { it.join('-') }
                .collect { it.endsWith('-') ? it[0..-2] : it }
        [(it):depList]
    }
    sbv = springBootVersion
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    constraints {
        plugins.each { key, version ->
            api "org.grails.plugins:${key}:\${${key}.version}"
        }
        profiles.each { key, version ->
            api "org.grails.profiles:${key}:\${${key}.version}"
        }

        dependencyVersions.each { dependency, artifacts ->
            artifacts.each {
                api "${dependency.group}:${it}:${dependency.version}"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def root = asNode()

                def dpm = root.appendNode('dependencyManagement')
                def deps = dpm.appendNode('dependencies')
                def ver = springBootVersion
                deps.appendNode('dependency').with {
                    appendNode('groupId', 'org.springframework.boot')
                    appendNode('artifactId', 'spring-boot-dependencies')
                    appendNode('version', "${ver}")
                    appendNode('type', 'pom')
                    appendNode('scope', 'import')
                }

                dependencyVersions.each { dependency, artifacts ->
                    artifacts.each { artifact ->
                        deps.appendNode('dependency').with {
                            appendNode('groupId', dependency.group)
                            appendNode('artifactId', artifact)
                            appendNode('version', "\${${dependency.names[0]}.version}")
                        }
                    }
                }
                def propsNode = root.appendNode('properties')

                dependencyVersions.each { dependency, artifacts ->
                    propsNode.appendNode("${dependency.names[0]}.version", version)
                }
                plugins.each { key, version ->
                    if (!isBuildSnapshot && version.endsWith("-SNAPSHOT")) {
                        throw new RuntimeException("Cannot have a snapshot dependency $version on a plugin [$key] for a release!")
                    }
                    propsNode.appendNode("${key}.version", version)
                }
                profiles.each { key, version ->
                    if (!isBuildSnapshot && version.endsWith("-SNAPSHOT")) {
                        throw new RuntimeException("Cannot have a snapshot dependency $version on a plugin [$key] for a release!")
                    }
                    propsNode.appendNode("${key}.version", version)
                }
            }
        }
    }
}