plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

def plugins = new Properties()
plugins.load(new StringReader(new File("$projectDir/plugins.properties").text))

def profiles = new Properties()
profiles.load(new StringReader(new File("$projectDir/profiles.properties").text))

ext {
    // When making changes in the dependencyVersions, remember to also update the Grails BOM Documentation:
    // https://docs.grails.org/snapshot/ref/Dependency%20Versions/Grails%20BOM.html
    dependencyVersions = """\
        com.fasterxml.jackson.core:jackson-databind::$jacksonVersion
        com.github.ben-manes.caffeine:caffeine::$caffeineVersion
        com.github.javaparser:javaparser-core::$javaParserCoreVersion
        com.h2database:h2::$h2Version
        commons-codec:commons-codec::$commonsCodecVersion
        io.methvin:directory-watcher::$directoryWatcherVersion
        jakarta.annotation:jakarta.annotation-api::$jakartaAnnotationApiVersion
        jakarta.inject:jakarta.inject-api::$jakartaInjectVersion
        jakarta.persistence:jakarta.persistence-api::$jakartaPersistenceVersion
        jakarta.servlet:jakarta.servlet-api::$servletApiVersion
        jakarta.xml.bind:jakarta.xml.bind-api::$jakartaXmlBindVersion
        jline:jline::$jlineVersion
        net.bytebuddy:byte-buddy::$bytebuddyVersion
        net.java.dev.jna:jna::$jnaVersion
        org.apache.ant:ant:,junit:$antVersion
        org.apache.commons:commons-text::$commonsTextVersion
        org.apache.groovy:groovy:ant,cli-picocli,console,dateutil,json,macro,nio,sql,swing,templates,test,test-junit5,xml:$groovyVersion
        org.apache.tomcat:tomcat-jdbc::$tomcatVersion
        org.asciidoctor:asciidoctorj::$asciidoctorjVersion
        org.aspectj:aspectjrt,aspectjweaver::$aspectjVersion
        org.fusesource.jansi:jansi::$jansiVersion
        com.github.spotbugs:spotbugs-annotations::$jsr305Version
        org.grails.plugins:async,events::$asyncVersion
        org.grails.plugins:converters::$legacyConvertersVersion
        org.grails.plugins:gsp::$gspVersion
        org.grails:grails-async,grails-events:gpars,rxjava,rxjava2:$asyncVersion
        org.grails:grails-datastore-gorm-hibernate5::$hibernateDatastoreVersion
        org.grails:grails-datastore:async,core,gorm,gorm-async,gorm-support,gorm-rx,gorm-test,gorm-validation,web:$datastoreVersion
        org.grails:grails-gdoc-engine::$gdocEngineVersion
        org.grails:grails-gradle-plugin::$grailsGradlePluginVersion
        org.grails:grails-testing-support,grails-gorm-testing-support,grails-web-testing-support::$testingSupportVersion
        org.grails:grails:grails-web-taglib,gsp,web-gsp:$gspVersion:gsp
        org.grails:views-json-testing-support::$viewsVersion
        org.hamcrest:hamcrest-core::$hamcrestVersion
        org.jsoup:jsoup::$jsoupVersion
        org.junit.jupiter:junit-jupiter:api,engine:$junitVersion
        org.junit.platform:junit-platform-runner::$junitPlatformRunnerVersion
        org.mongodb:bson:,record-codec:$mongodbJavaDriverVersion
        org.mongodb:mongodb-driver:core,sync:$mongodbJavaDriverVersion
        org.objenesis:objenesis::$objenesisVersion
        org.slf4j:jcl-over-slf4j::$slf4jVersion
        org.slf4j:slf4j:api,simple:$slf4jVersion
        org.spockframework:spock-core,spock-spring::$spockVersion
        org.springframework.boot:spring-boot-cli::$springBootVersion
        org.springframework:springloaded::$springLoadedVersion
        org.xhtmlrenderer:flying-saucer-pdf-openpdf::$xhtmlRendererVersion
        org.yaml:snakeyaml::$snakeyamlVersion
    """.trim().readLines().collect { it.trim() }.collect {
        def info = it.toString().split(':')
        def dep = [group: info[0], names: info[1].split(','), modules: info[2].split(','), version: info[3]]
        dep.name = info.length == 5? info[4] : dep.names[0] // allow name override
        dep
    }.collectEntries {
        def depList = GroovyCollections
                .combinations(it.names, it.modules)
                .collect { it.join('-') }
                .collect { it.endsWith('-') ? it[0..-2] : it }
        [(it):depList]
    }

    def dependencyList = dependencyVersions.collectMany { dependency, artifacts ->
        artifacts.collect { artifactId ->
            [groupId: dependency.group, artifactId: artifactId, version: "\${${dependency.name}.version}",
             versionValue: dependency.version, name: dependency.name]
        }
    }

    pluginsAndProfiles =
        plugins.collect { [group: "org.grails.plugins", artifact:it.key, version: it.value, name: "plugins-${it.key}"] } +
        profiles.collect { [group: "org.grails.profiles", artifact:it.key, version: it.value, name: "profiles-${it.key}"] }

    allDependencies = (pluginsAndProfiles + dependencyList).sort { it.name + it.artifactId }
}

allDependencies.each {
    if (!isBuildSnapshot && version.endsWith("-SNAPSHOT")) {
        throw new RuntimeException("Cannot have a snapshot dependency $it.version on a $it.name [$it.groupId] for a release!")
    }
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    constraints {
        allDependencies.each {
            api "${it.groupId}:${it.artifactId}:${it.versionValue}"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def root = asNode()

                def propsNode = root.appendNode('properties')
                propsNode.appendNode('spring-boot.version', springBootVersion)
                (dependencyVersions.collect { [ versionValue:it.key.version, name: "${it.key.name}.version" ] } +
                        pluginsAndProfiles
                ).sort { it.name }
                .groupBy { it.name }.collect { it.value.first() } // remove duplicates
                .each {
                    propsNode.appendNode(it.name, it.versionValue)
                }

                def appendNodes = { node, attrs ->
                    attrs.each {
                        node.appendNode(it.key, it.value)
                    }
                }

                def dpm = root.appendNode('dependencyManagement')
                def deps = dpm.appendNode('dependencies')
                appendNodes(deps.appendNode('dependency'), [
                    groupId: 'org.springframework.boot',
                    artifactId: 'spring-boot-dependencies',
                    version: '${spring-boot.version}',
                    type: 'pom',
                    scope: 'import'
                ])
                
                allDependencies.collect { it.subMap('groupId', 'artifactId', 'version') }.each {
                    appendNodes(deps.appendNode('dependency'), it)
                }
            }
        }
    }
}