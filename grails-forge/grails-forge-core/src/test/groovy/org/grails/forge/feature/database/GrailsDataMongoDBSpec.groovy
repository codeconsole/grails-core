/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

package org.grails.forge.feature.database

import org.grails.forge.ApplicationContextSpec
import org.grails.forge.BuildBuilder
import org.grails.forge.application.generator.GeneratorContext
import org.grails.forge.feature.Features
import org.grails.forge.fixture.CommandOutputFixture
import org.grails.forge.options.DevelopmentReloading
import org.grails.forge.options.GormImpl
import org.grails.forge.options.JdkVersion
import org.grails.forge.options.Options
import org.grails.forge.options.ServletImpl

class GrailsDataMongoDBSpec extends ApplicationContextSpec implements CommandOutputFixture {

    void "test Mongo gorm features"() {
        when:
        Features features = getFeatures(['gorm-mongodb'])

        then:
        features.contains("gorm-mongodb")
    }

    void "test Mongo gorm with Embedded MongoDB features "() {
        when:
        Features features = getFeatures(['gorm-mongodb'])

        then:
        features.contains("gorm-mongodb")
    }

    void "test dependencies are present for gradle"() {
        when:
        String template = new BuildBuilder(beanContext)
                .features(["gorm-mongodb"])
                .render()

        then:
        template.contains("implementation \"org.apache.grails:grails-data-mongodb\"")
        template.contains("implementation \"org.apache.grails:grails-data-mongodb-embedded\"")

        and: 'flapdoodle is the application\'s own dependency, so it carries a version'
        template.contains("implementation \"de.flapdoodle.embed:de.flapdoodle.embed.mongo:\$flapdoodleVersion\"")
    }

    void "test config"() {
        when:
        GeneratorContext ctx = buildGeneratorContext(['gorm-mongodb'])

        then:
        ctx.configuration.containsKey("grails.mongodb.url")
    }

    void "test the embedded MongoDB is enabled in every environment"() {
        when:
        GeneratorContext ctx = buildGeneratorContext(['gorm-mongodb'])

        then: 'each environment starts its own server, so a generated app runs with no MongoDB installed'
        ctx.configuration.get("environments.development.embedded.mongodb.enabled") == true
        ctx.configuration.get("environments.test.embedded.mongodb.enabled") == true
        ctx.configuration.get("environments.production.embedded.mongodb.enabled") == true

        and: 'development and test start instantly and download nothing'
        ctx.configuration.get("environments.development.embedded.mongodb.backend") == 'in-memory'
        ctx.configuration.get("environments.test.embedded.mongodb.backend") == 'in-memory'

        and: 'only production runs a real mongod, because it is the backend that can persist'
        ctx.configuration.get("environments.production.embedded.mongodb.backend") == 'flapdoodle'
        ctx.configuration.get("environments.production.embedded.mongodb.database-dir") == './prodDb'
        !ctx.configuration.containsKey("environments.development.embedded.mongodb.database-dir")
        !ctx.configuration.containsKey("environments.test.embedded.mongodb.database-dir")
    }

    void "test no initializer is copied into the generated application"() {
        when:
        Map<String, String> output = generate(['gorm-mongodb'])

        then: 'the embedded support arrives as a dependency, not as generated source'
        !output.keySet().any { it.endsWith('EmbeddedMongoConfig.groovy') }
        !output.containsKey('src/main/resources/META-INF/spring.factories')
    }

    void "test a SQL driver combined with MongoDB still adds Hibernate 5 as the default SQL implementation"() {
        given:
        Options options = new Options(DevelopmentReloading.DEFAULT_OPTION, GormImpl.MONGODB, ServletImpl.DEFAULT_OPTION, JdkVersion.DEFAULT_OPTION)

        when:
        Features features = getFeatures(['gorm-mongodb', 'postgres'], options)

        then:
        features.contains("gorm-mongodb")
        features.contains("postgres")
        features.contains("gorm-hibernate5")
        !features.contains("gorm-hibernate7")

        when:
        String template = new BuildBuilder(beanContext)
                .features(['gorm-mongodb', 'postgres'])
                .gormImpl(GormImpl.MONGODB)
                .render()

        then:
        template.contains("implementation \"org.apache.grails:grails-data-mongodb\"")
        template.contains("implementation \"org.apache.grails:grails-data-hibernate5\"")
        template.contains("runtimeOnly \"org.postgresql:postgresql\"")

        when:
        GeneratorContext ctx = buildGeneratorContext(['gorm-mongodb', 'postgres'], options)

        then:
        ctx.configuration.containsKey("grails.mongodb.url")
        ctx.configuration.get("dataSource.driverClassName") == 'org.postgresql.Driver'
    }

}
