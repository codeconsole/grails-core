/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package grails.orm.bootstrap

import javax.sql.DataSource

import grails.gorm.annotation.Entity
import org.grails.datastore.mapping.core.connections.ConnectionSource
import org.grails.orm.hibernate.HibernateDatastore
import org.hibernate.Session
import org.hibernate.SessionFactory
import org.hibernate.dialect.H2Dialect
import org.springframework.context.ConfigurableApplicationContext
import org.springframework.jdbc.datasource.DriverManagerDataSource
import org.springframework.transaction.PlatformTransactionManager
import spock.lang.Specification
import spock.lang.Unroll

/**
 * Created by graemerocher on 29/01/14.
 */
class HibernateDatastoreSpringInitializerSpec extends Specification{

    void "Test configure multiple data sources"() {
        given:"An initializer instance"
        Map config = [
                'dataSource.url':"jdbc:h2:mem:people;LOCK_TIMEOUT=10000",
                'dataSource.dialect': H2Dialect.name,
                'dataSource.formatSql': 'true',
                'hibernate.flush.mode': 'COMMIT',
                'hibernate.cache.queries': 'true',
                'hibernate.hbm2ddl.auto': 'create',
                'dataSources.books.url':"jdbc:h2:mem:books;LOCK_TIMEOUT=10000",
                'dataSources.moreBooks.url':"jdbc:h2:mem:moreBooks;LOCK_TIMEOUT=10000"
        ]
        def datastoreInitializer = new HibernateDatastoreSpringInitializer(config, Person, Book, Author)

        when:"the application is configured"
        def applicationContext = datastoreInitializer.configure()
        println applicationContext.getBeanDefinitionNames()

        then:"Each session factory has the correct number of persistent entities"
        applicationContext.getBeansOfType(PlatformTransactionManager).size() == 3
        applicationContext.getBean("sessionFactory", SessionFactory).metamodel.entities.size() == 2
        applicationContext.getBean("sessionFactory", SessionFactory).metamodel.entity(Person.name)
        applicationContext.getBean("sessionFactory", SessionFactory).metamodel.entity(Author.name)
        applicationContext.getBean("sessionFactory_books", SessionFactory).metamodel.entities.size() == 2
        applicationContext.getBean("sessionFactory_books", SessionFactory).metamodel.entity(Book.name)
        applicationContext.getBean("sessionFactory_books", SessionFactory).metamodel.entity(Author.name)
        applicationContext.getBean("sessionFactory_moreBooks", SessionFactory).metamodel.entities.size() == 2
        applicationContext.getBean("sessionFactory_moreBooks", SessionFactory).metamodel.entity(Book.name)
        applicationContext.getBean("sessionFactory_moreBooks", SessionFactory).metamodel.entity(Author.name)

        and:"Each domain has the correct data source(s)"
        Person.withNewSession { Person.count() == 0 }
        Person.withNewSession {  Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:people"
            return true
        }
        Book.withNewSession { Book.count() == 0 }
        Book.withNewSession { Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:books"
            return true
        }
        Book.moreBooks.withNewSession { Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:moreBooks"
            return true
        }
        Author.withNewSession { Author.count() == 0 }
        Author.withNewSession { Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:people"
            return true
        }
        Author.books.withNewSession { Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:books"
            return true
        }
        Author.moreBooks.withNewSession { Session s ->
            assert s.connection().metaData.getURL() == "jdbc:h2:mem:moreBooks"
            return true
        }

    }

    @Unroll
    void "the default connection source is the first of the data sources when #configured"() {
        when: 'an initializer is created'
        def datastoreInitializer = new HibernateDatastoreSpringInitializer(config, Person)

        then: 'the default connection source comes first, followed by the additional data sources'
        datastoreInitializer.dataSources.first() == ConnectionSource.DEFAULT
        datastoreInitializer.dataSources == expected as Set

        where:
        configured                        | config                                                || expected
        'nothing is configured'           | [:]                                                   || [ConnectionSource.DEFAULT]
        'only dataSource is configured'   | ['dataSource.url': 'jdbc:h2:mem:orderDefault']        || [ConnectionSource.DEFAULT]
        'only dataSources are configured' | ['dataSources.moreBooks.url': 'jdbc:h2:mem:moreBooks',
                                             'dataSources.books.url': 'jdbc:h2:mem:books']        || [ConnectionSource.DEFAULT, 'moreBooks', 'books']
        'both are configured'             | ['dataSources.moreBooks.url': 'jdbc:h2:mem:moreBooks',
                                             'dataSource.url': 'jdbc:h2:mem:orderDefault',
                                             'dataSources.books.url': 'jdbc:h2:mem:books']        || [ConnectionSource.DEFAULT, 'moreBooks', 'books']
    }

    void "the default data source is registered as the dataSource bean when no dataSource is configured"() {
        given: 'an initializer without dataSource configuration'
        def datastoreInitializer = new HibernateDatastoreSpringInitializer([:], Person)

        when: 'the application is configured'
        def applicationContext = (ConfigurableApplicationContext) datastoreInitializer.configure()
        def hibernateDatastore = applicationContext.getBean(HibernateDatastore)

        then: 'the data source GORM uses is available by name and by type'
        applicationContext.getBeansOfType(DataSource).keySet() == ['dataSource'] as Set
        applicationContext.getBean('dataSource', DataSource).is(hibernateDatastore.dataSource)
        applicationContext.getBean('dataSource', DataSource).connection.withCloseable { it.metaData.URL } ==
                'jdbc:h2:mem:grailsDB'

        cleanup:
        applicationContext?.close()
    }

    void "the default data source is registered as the dataSource bean when only additional data sources are configured"() {
        given: 'an initializer configuring only an additional data source'
        def datastoreInitializer = new HibernateDatastoreSpringInitializer([
                'dataSources.books.url': 'jdbc:h2:mem:defaultDataSourceBeanBooks;LOCK_TIMEOUT=10000'
        ], Person)

        when: 'the application is configured'
        def applicationContext = (ConfigurableApplicationContext) datastoreInitializer.configure()

        then: 'both the default and the additional data source are beans'
        applicationContext.getBeansOfType(DataSource).keySet() == ['dataSource', 'dataSource_books'] as Set
        applicationContext.getBean('dataSource', DataSource).connection.withCloseable { it.metaData.URL } ==
                'jdbc:h2:mem:grailsDB'
        applicationContext.getBean('dataSource_books', DataSource).connection.withCloseable { it.metaData.URL } ==
                'jdbc:h2:mem:defaultDataSourceBeanBooks'

        cleanup:
        applicationContext?.close()
    }

    void "the data source passed to configureForDataSource remains the dataSource bean"() {
        given: 'a data source and an initializer without dataSource configuration'
        def dataSource = new DriverManagerDataSource('jdbc:h2:mem:configureForDataSource;LOCK_TIMEOUT=10000')
        def datastoreInitializer = new HibernateDatastoreSpringInitializer([:], Person)

        when: 'the application is configured for that data source'
        def applicationContext = (ConfigurableApplicationContext) datastoreInitializer.configureForDataSource(dataSource)

        then: 'the dataSource bean is the given data source'
        applicationContext.getBean('dataSource', DataSource).is(dataSource)

        cleanup:
        applicationContext?.close()
    }
}
@Entity
class Person {
    Long id
    Long version
    String name

    static constraints = {
        name blank:false
    }
}

@Entity
class Book {
    Long id
    Long version
    String name

    static mapping = {
        datasources( ['books', 'moreBooks'] )
    }
    static constraints = {
        name blank:false
    }
}

@Entity
class Author {
    Long id
    Long version
    String name

    static mapping = {
        datasource 'ALL'
    }
    static constraints = {
        name blank:false
    }
}
